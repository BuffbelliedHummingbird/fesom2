! PDAF_V2.0

! $Id: generate_covar.F90 1604 2016-05-30 06:42:16Z lnerger $

!Modified by QT       2017-12-12 ---- for AWI-CM 
!Modified by QT       2017-12-19
!Modified by Frauke B 2022-02-22 ---- for FESOM2.1

! !Program: generate_covar --- Compute covariance matrix from state trajectory
PROGRAM generate_covar

! !DESCRIPTION:
! This programm to computes a covariance matrix from a
! model trajectory of FESOM2.1. The matrix is decomposed in
! EOFs and stored in form of eigenvalues and eigenvectors.
!
! The matrix is generated by a singular value decomposition
! of the perturbation matrix of a long state trajectory
! about its long time mean.
!
! 1) the state vector is structured (z, u, v, w, T, S, a_ice, MLD1, [biogeochemistry])^T;
! 2) u and v structure is according to u and v being interpolated from elements onto nodes
! 3) output frequency is every day;
! 4) only one output file, including the running mean, singular values and singular vectors
! 5) MLD is filled with null (model diagnostic)
!
! !NOTES:
! Type "ulimit -s unlimited" before executing in case of segmentation
! fault error on Ollie.

! Three arrays (svdU, states, run_meanstate) each need:
! dimstate = 30192204; maxtimes = 73; REAL = 4 bytes
! allocate approx. 8800 MB

! !USES:
  IMPLICIT NONE

  INCLUDE 'netcdf.inc'


! Local variables
  
  CHARACTER(len=120) :: inpath, outpath                 ! File paths
  CHARACTER(len=120) :: infile_u, infile_v, infile_w    ! File names of velocity -- input
  CHARACTER(len=120) :: infile_s, infile_z, infile_t    ! File names of salinity, sea surface height and temperature -- input
  CHARACTER(len=120) :: infile_i                        ! File names of sea-ice concentration -- input
  CHARACTER(len=120) :: outfile                         ! File names   -- output
  CHARACTER(len=150) :: ncfile_in_u, ncfile_in_v, ncfile_in_w    ! File names including path
  CHARACTER(len=150) :: ncfile_in_s, ncfile_in_z, ncfile_in_t    ! File names including path
  CHARACTER(len=150) :: ncfile_in_i                              ! File names including path
  CHARACTER(len=150) :: ncfile_out                               ! File name including path
  CHARACTER(len=150) :: attstr                          ! Attribute string for NC (NetCDF) output
  
  INTEGER :: i, j, k, s, iter, maxtimes, rank, b        ! Counters
  INTEGER :: ncid_in_u, ncid_in_v, ncid_in_w            ! NC file IDs
  INTEGER :: ncid_in_s, ncid_in_z, ncid_in_t            ! NC file IDs
  INTEGER :: ncid_in_i                                  ! NC file IDs
  INTEGER :: ncid_out                                   ! NC file IDs
  INTEGER :: id_dim, id_time, id_state                  ! NC dimension and variable IDs
  INTEGER :: id_u, id_v, id_w                           ! Field variable IDs
  INTEGER :: id_s, id_z, id_t                           ! Field variable IDs
  INTEGER :: id_i                                       ! Field variable IDs
  INTEGER :: id_mu, id_mv, id_mw                        ! Mean field variables IDs
  INTEGER :: id_mz, id_ms, id_mt                        ! Mean field variables IDs
  INTEGER :: id_mi                                      ! Mean filed variables IDs
  INTEGER :: id_svdu, id_svdv, id_svdw                  ! Singular vector IDs
  INTEGER :: id_svdz, id_svds, id_svdt                  ! Singular vector IDs
  INTEGER :: id_svdi                                    ! Singular vector IDs
  INTEGER :: id_sigma, id_svec                          ! NC dimension and variable IDs
  INTEGER :: dimid_rank, dimid_state, dimid_one, dimid_nfields ! NC dimension and variable IDs
  INTEGER :: dimid_nod2, dimid_nz, dimid_elem           ! NC dimension and variable IDs
  INTEGER :: dimid_2D, dimid_tracer3D, dimid_w          ! NC dimension and variable IDs
  INTEGER :: dimid_maxtimes                             ! NC dimension and variable IDs
  INTEGER :: nod2, nz, elem                             ! Number of 2d nodes, depth levels and 2d elements
  INTEGER :: steps, dim_state                           ! Dimensions
  INTEGER :: stat(100)                                  ! Status for NC operations
  INTEGER :: countv2(2), startv2(2)                     ! Vectors for NC operations (2D)
  INTEGER :: countv3(3), startv3(3)                     ! Vectors for NC operations (3D)
  INTEGER :: dimids(2)                                  ! Vector for NC operations 
  INTEGER :: nfields                                    ! Number of fields
  INTEGER :: id_field                                   ! Field
  INTEGER :: hwindow                                    ! Half time window
  INTEGER :: ostep                                      ! Time step                             
  INTEGER :: status                                     ! Status Flag for PDAF routine
  
  INTEGER, ALLOCATABLE :: dim_fields(:)                              ! Size of field
  REAL, ALLOCATABLE    :: stddev(:)                                  ! STDDEV of field
  INTEGER, ALLOCATABLE :: offsets(:)                                 ! Offset of fields
  
  REAL :: limit                                         ! Lower limit for singular values
  
  REAL, ALLOCATABLE :: times(:)                         ! Array of times from NC file
  REAL, ALLOCATABLE :: states(:, :)                     ! State trajectory
  REAL, ALLOCATABLE :: run_meanstate (:, :)             ! Running mean state
  REAL, ALLOCATABLE :: meanstate(:)                     ! Mean state of trajectory
  REAL,ALLOCATABLE,DIMENSION(:) :: svals                ! field of singular values
  REAL,ALLOCATABLE,DIMENSION(:,:) :: svdU               ! left singular vectors

  ! Controls for PDAF_eofcovar
  INTEGER :: remove_mean  ! (1) Let PDAF_eofcovar compute and subtract the mean state;
                          ! (0) mean already removed from trajectory
  INTEGER :: do_mv        ! (1) to perform multivariate normalization; (0) no normalization
  
  ! Variables for interpolation of u and v
  REAL, ALLOCATABLE ::    u_orig(:), v_orig(:)        ! Velocities before interpolation
  INTEGER ::              l, n, el, c                 ! Counters
  INTEGER, ALLOCATABLE :: nod_in_elem2D_num(:), &     ! Number of elements sharing a vertex
                          nod_in_elem2D(:,:), &       ! Elements sharing vertex
                          nlevels(:), &               ! Number of depth levels at elements considering topography
                          nlevels_nod2D(:)            ! Number of depth levels at nodes considering topography
  INTEGER(kind=8), ALLOCATABLE :: elem_area(:)        ! Area of elements (rounded to square meters)
  INTEGER ::              nod_in_elem2D_max           ! Maximum number of elements sharing a vertex
  REAL ::                 tvol, &                     ! Area sum of elements sharing a vertex
                          tx, ty                      ! To sum up velocities weighted by area

  CHARACTER(len=120) ::   file_nod_in_elem, &         ! File names
                          file_elem_area, &
                          file_nlevels, &
                          file_nlevels_nod2D
  INTEGER ::              fID_nod_in_elem, &          ! File IDs
                          fID_elem_area, &
                          fID_nlevels, &
                          fID_nlevels_nod2D
                          
! --------------------
! For biogeochemistry:
! --------------------
                          
  integer :: biomin, biomax
  
  TYPE field_ids
     INTEGER :: MLD1    ! (Dummy data)
     INTEGER :: PhyChl
     INTEGER :: DiaChl
     INTEGER :: DIC
     INTEGER :: DOC
     INTEGER :: Alk
     INTEGER :: DIN
     INTEGER :: DON
     INTEGER :: O2
     INTEGER :: pCO2s
     INTEGER :: CO2f
     INTEGER :: PhyN
     INTEGER :: PhyC
     INTEGER :: DiaN
     INTEGER :: DiaC
     INTEGER :: DiaSi
     INTEGER :: PAR
     INTEGER :: NPPn   ! (Dummy data)
     INTEGER :: NPPd   ! (Dummy data)
     INTEGER :: TChl   ! Total chlorophyll = PhyChl + DiaChl
     INTEGER :: TDN    ! Total dissolved N = DIN + DON
     INTEGER :: HetC
     INTEGER :: DetC
     INTEGER :: TOC    ! Total organic carbon: PhyC + DiaC + DetC + DOC + HetC
  END TYPE field_ids
  
  TYPE(field_ids) :: idbio                ! Type variable holding field IDs in state vector
                          
  type state_field
   integer :: ndims = 0                   ! Number of field dimensions (1 or 2)
   character(len= 10) :: variable = ''    ! Name of field
   character(len=200) :: filename = ''    ! File name input incl. path
   integer :: ncid = 0                    ! ID nc-file input
   integer :: fid = 0                     ! Field ID (in/output file)
   integer :: mid = 0                     ! Mean field ID (output file)
   integer :: sid = 0                     ! Singular vector ID
   logical :: dfield = .false.            ! Diagnostic fields derived from model fields
                                          ! (e.g. total chlorophyll,
                                          ! derived from DiaChl + PhyChl)
  end type state_field

  type(state_field), allocatable :: biofields(:) ! Type variable holding the
                                                 ! definitions of model fields
                                                 

  ! File IDs:
  ! fID_nod_in_elem   = 90
  ! fID_elem_area     = 91
  ! fID_nlevels       = 92
  ! fID_nlevels_nod2D = 93
  
  ! ncid_in_u = 10
  ! ncid_in_v = 11
  ! ncid_in_w = 12 
  ! ncid_in_s = 13
  ! ncid_in_z = 14
  ! ncid_in_t = 15
  ! ncid_in_i = 16
  
  ! ncid_out = 70

! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Number of fields in state vector
  nfields = 31

  ! Maximum number of time slices to consider
  maxtimes = 72

  ! Set do_mv=1 to activate normalization; 0 to run without normalization
  do_mv = 0  

  ! Path to and name of file holding model trajectory
  ! inpath = '/work/ollie/frbunsen/model_runs/fesom2/test_control/'
  inpath = '/albedo/work/projects/p_recompdaf/frbunsen/modelruns/fesom2/yr2016/'
  infile_z = 'ssh.fesom.2016.nc'         ! sea surface elevation (2D)
  infile_u = 'u.fesom.2016.nc'           ! zonal velocity (3D)
  infile_v = 'v.fesom.2016.nc'           ! meridional velocity (3D)
  infile_w = 'w.fesom.2016.nc'           ! vertical velocity (3D)
  infile_t = 'temp.fesom.2016.nc'        ! temperature (3D)
  infile_s = 'salt.fesom.2016.nc'        ! salinity (3D)
  infile_i = 'a_ice.fesom.2016.nc'       ! sea-ice concentration (2D)

  ! Path to and name of output file holding covariance matrix
  ! outpath = '/work/ollie/frbunsen/model_runs/fesom2/test_control/cov/'
  outpath = '/albedo/work/projects/p_recompdaf/frbunsen/modelruns/cov/'
  outfile = 'cov.nc'

  ! Lower limit for eigenvalue
  limit = 1.0e-12

  ! Half time window for running mean (2*irange+1)
  hwindow = 3 ! 6 days
  
  ! Time step (only consider every x-th value of model output)
  ostep = 5  ! every 5th day in daily output

  ! Note: multivariate normalization can be useful if there are several fields
  !       with difference variances. Here, we have 7 fields.
  
  ! Maximum number of elements sharing a vertex in CORE2 mesh
  nod_in_elem2D_max = 9
  
  ! ************************************************
  ! *** Configuration biogeochemistry            ***
  ! ************************************************
  
  ! Number of biogeochemistry fields and their indeces in state vector
  biomin = 8
  biomax = 31
  allocate(biofields(nfields))
  
  idbio% MLD1   = 8
  idbio% PhyChl = 9
  idbio% DiaChl = 10
  idbio% DIC    = 11
  idbio% DOC    = 12
  idbio% Alk    = 13
  idbio% DIN    = 14
  idbio% DON    = 15
  idbio% O2     = 16
  idbio% pCO2s  = 17
  idbio% CO2f   = 18
  idbio% PhyN   = 19
  idbio% PhyC   = 20
  idbio% DiaN   = 21
  idbio% DiaC   = 22
  idbio% DiaSi  = 23
  idbio% PAR    = 24
  idbio% NPPn = 25
  idbio% NPPd = 26
  idbio% TChl   = 27 ! PhyChl + DiaChl
  idbio% TDN    = 28 ! DIN + DON
  idbio% HetC   = 29
  idbio% DetC   = 30
  idbio% TOC    = 31 ! PhyC + DiaC + DetC + DOC + HetC
  

  biofields(idbio% MLD1) % ndims = 1
  biofields(idbio% MLD1) % variable = 'MLD1'
  biofields(idbio% MLD1) % dfield = .true.
  
  biofields(idbio% PhyChl) % ndims = 2
  biofields(idbio% PhyChl) % variable = 'PhyChl'

  biofields(idbio% DiaChl) % ndims = 2
  biofields(idbio% DiaChl) % variable = 'DiaChl'

  biofields(idbio% DIC) % ndims = 2
  biofields(idbio% DIC) % variable = 'DIC' 

  biofields(idbio% DOC) % ndims = 2
  biofields(idbio% DOC) % variable = 'DOC'

  biofields(idbio% Alk) % ndims = 2
  biofields(idbio% Alk) % variable = 'Alk'

  biofields(idbio% DIN) % ndims = 2
  biofields(idbio% DIN) % variable = 'DIN'

  biofields(idbio% DON) % ndims = 2
  biofields(idbio% DON) % variable = 'DON'

  biofields(idbio% O2) % ndims = 2
  biofields(idbio% O2) % variable = 'O2'

  biofields(idbio% pCO2s) % ndims = 1
  biofields(idbio% pCO2s) % variable = 'pCO2s'

  biofields(idbio% CO2f) % ndims = 1
  biofields(idbio% CO2f) % variable = 'CO2f'

  biofields(idbio% PhyN) % ndims = 2
  biofields(idbio% PhyN) % variable = 'PhyN'
  
  biofields(idbio% PhyC) % ndims = 2
  biofields(idbio% PhyC) % variable = 'PhyC'
  
  biofields(idbio% DiaN) % ndims = 2
  biofields(idbio% DiaN) % variable = 'DiaN'
  
  biofields(idbio% DiaC) % ndims = 2
  biofields(idbio% DiaC) % variable = 'DiaC'
  
  biofields(idbio% DiaSi) % ndims = 2
  biofields(idbio% DiaSi) % variable = 'DiaSi'
  
  biofields(idbio% PAR) % ndims = 2
  biofields(idbio% PAR) % variable = 'PAR'
  
  biofields(idbio% NPPn) % ndims = 2
  biofields(idbio% NPPn) % variable = 'NPPn'
  biofields(idbio% NPPn) % dfield = .true.
  
  biofields(idbio% NPPd) % ndims = 2
  biofields(idbio% NPPd) % variable = 'NPPd'
  biofields(idbio% NPPd) % dfield = .true.
  
  biofields(idbio% TChl) % ndims = 2
  biofields(idbio% TChl) % variable = 'TChl'
  biofields(idbio% TChl) % dfield = .true.
  
  biofields(idbio% TDN) % ndims = 2
  biofields(idbio% TDN) % variable = 'TDN'
  biofields(idbio% TDN) % dfield = .true.
  
  biofields(idbio% HetC) % ndims = 2
  biofields(idbio% HetC) % variable = 'HetC'
  
  biofields(idbio% DetC) % ndims = 2
  biofields(idbio% DetC) % variable = 'DetC'
  
  biofields(idbio% TOC) % ndims = 2
  biofields(idbio% TOC) % variable = 'TOC'
  biofields(idbio% TOC) % dfield = .true.

  
  do b=biomin, biomax
    if (biofields(b) % dfield) CYCLE
    biofields(b) % filename = trim(inpath)//trim(biofields(b) % variable)//'.fesom.2016.nc'
  enddo

! ************************************************
! *** Init                                     ***
! ************************************************

  allocate(dim_fields(nfields))
  allocate(stddev(nfields)) 
  allocate(offsets(nfields))


  WRITE (*,'(10x,a)')  '*******************************************'
  WRITE (*,'(10x,a)')  '*             GENERATE_COVAR              *'
  WRITE (*,'(10x,a)')  '*                                         *'
  WRITE (*,'(10x,a)')  '*    Compute covariance matrix and mean   *'
  WRITE (*,'(10x,a)')  '*     state from a sequence of states.    *'
  WRITE (*,'(10x,a)')  '*                                         *'
  WRITE (*,'(10x,a)')  '*   Write covar matrix as scaled eigen-   *'
  WRITE (*,'(10x,a)')  '*    vectors and singular values into     *'
  WRITE (*,'(10x,a)')  '*              NetCDF file                *'
  WRITE (*,'(10x,a/)') '*******************************************'

  ncfile_in_z = TRIM(inpath)//TRIM(infile_z)
  WRITE (*,*) 'Read sea surface height trajectory from file: ',TRIM(ncfile_in_z)
  ncfile_in_u = TRIM(inpath)//TRIM(infile_u)
  WRITE (*,*) 'Read u-velocity trajectory from file: ',TRIM(ncfile_in_u)
  ncfile_in_v = TRIM(inpath)//TRIM(infile_v)
  WRITE (*,*) 'Read v-velocity trajectory from file: ',TRIM(ncfile_in_v)
  ncfile_in_w = TRIM(inpath)//TRIM(infile_w)
  WRITE (*,*) 'Read w-velocity trajectory from file: ',TRIM(ncfile_in_w)
  ncfile_in_t = TRIM(inpath)//TRIM(infile_t)
  WRITE (*,*) 'Read temperature trajectory from file: ',TRIM(ncfile_in_t)
  ncfile_in_s = TRIM(inpath)//TRIM(infile_s)
  WRITE (*,*) 'Read salinity trajectory from file: ',TRIM(ncfile_in_s)
  ncfile_in_i = TRIM(inpath)//TRIM(infile_i)
  WRITE (*,*) 'Read sea-ice concentration trajectory from file: ',TRIM(ncfile_in_i)
  
  do b=biomin, biomax
    if (biofields(b) % dfield) CYCLE
    write(*,*) 'Read ',trim(biofields(b) % variable),' trajectory from file: ',trim(biofields(b) % filename)
  enddo

  ncfile_out = TRIM(outpath)//TRIM(outfile)
  WRITE (*,*) 'Write output to file: ',TRIM(ncfile_out)
  
! ***********************************************************
! *** Read mesh info to interpolate velocities            ***
! ***********************************************************
  WRITE (*,*) 'Reading mesh info to interpolate velocities:'
  
  nod2              = 126858
  elem              = 244659
  
  ALLOCATE(nod_in_elem2D(nod_in_elem2D_max,nod2))
  ALLOCATE(nod_in_elem2D_num(nod2))
  ALLOCATE(nlevels(elem))
  ALLOCATE(nlevels_nod2D(nod2))
  ALLOCATE(elem_area(elem))

  file_nod_in_elem   = 'elems_at_node.txt'
  file_nlevels       = 'nlevels.txt'
  file_nlevels_nod2D = 'nlevels_nod2D.txt'
  file_elem_area     = 'elem_area.txt'

  WRITE (*,*) ' - Reading file_nod_in_elem'
  open(fID_nod_in_elem, file=trim(file_nod_in_elem))
  DO l = 1,nod2
     read(fID_nod_in_elem,*)  nod_in_elem2D_num(l)
     read(fID_nod_in_elem,*)  nod_in_elem2D(:nod_in_elem2D_num(l),l)
     
     nod_in_elem2D(:nod_in_elem2D_num(l),l) = nod_in_elem2D(:nod_in_elem2D_num(l),l) + 1
  END DO
  close(fID_nod_in_elem)

  WRITE (*,*) ' - Reading in file_elem_area'
  open(fID_elem_area, file=trim(file_elem_area))
  read(fID_elem_area,*) elem_area
  close(fID_elem_area)

  WRITE (*,*) ' - Reading in file_nlevels'
  open(fID_nlevels, file=trim(file_nlevels))
  read(fID_nlevels,*) nlevels
  close(fID_nlevels)
  
  WRITE (*,*) ' - Reading in file_nlevels_nod2D'
  open(fID_nlevels_nod2D, file=trim(file_nlevels_nod2D))
  read(fID_nlevels_nod2D,*) nlevels_nod2D
  close(fID_nlevels)

! ***********************************************************
! *** Open u-velocity trajectory file and read dimensions ***
! ***********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_u), NF_NOWRITE, ncid_in_u)
  s = s + 1

  ! Get dimensions
  stat(s) = NF_INQ_DIMID(ncid_in_u, 'elem', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in_u, id_dim, elem)
  s = s + 1
  stat(s) = NF_INQ_DIMID(ncid_in_u, 'time', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in_u, id_dim, steps)
  s = s + 1
  stat(s) = NF_INQ_DIMID(ncid_in_u, 'nz1', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in_u, id_dim, nz)

  ! Initialize time array
  ALLOCATE(times(steps))
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_u, 'time', id_time)
  s = s + 1
  stat(s) = NF_GET_VAR_DOUBLE(ncid_in_u, id_time, times)

  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_u, 'u', id_u)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of u-velocity file, no.', i
  END DO


! ***********************************************************
! *** Open v-velocity trajectory file and read dimensions ***
! ***********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_v), NF_NOWRITE, ncid_in_v)
 
  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_v, 'v', id_v)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of v-velocity file, no.', i
  END DO


! ***********************************************************
! *** Open w-velocity trajectory file and read dimensions ***
! ***********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_w), NF_NOWRITE, ncid_in_w)
  
  ! Get dimensions:
  s = s + 1
  stat(s) = NF_INQ_DIMID(ncid_in_w, 'nod2', id_dim)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(ncid_in_w, id_dim, nod2)
 
  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_w, 'w', id_w)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of w-velocity file, no.', i
  END DO


! *******************************************************************
! *** Open sea surface height trajectory file and read dimensions ***
! *******************************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_z), NF_NOWRITE, ncid_in_z)
  s = s + 1

  ! Get state variable ID
  stat(s) = NF_INQ_VARID(ncid_in_z, 'ssh', id_z)
  s = s + 1

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of sea surface height file, no.', i
  END DO
  
! **********************************************************************
! *** Open sea-ice concentration trajectory file and read dimensions ***
! **********************************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_i), NF_NOWRITE, ncid_in_i)
  s = s + 1

  ! Get state variable ID
  stat(s) = NF_INQ_VARID(ncid_in_i, 'a_ice', id_i)
  s = s + 1

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of sea-ice concentration file, no.', i
  END DO


! ***********************************************************
! *** Open salinity trajectory file and read dimensions ***
! ***********************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_s), NF_NOWRITE, ncid_in_s)
 
  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_s, 'salt', id_s)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of salinity file, no.', i
  END DO


! ******************************************************************
! *** Open ocean temperature trajectory file and read dimensions ***
! ******************************************************************

  s = 1
  stat(s) = NF_OPEN(TRIM(ncfile_in_t), NF_NOWRITE, ncid_in_t)
 
  ! Get state variable ID
  s = s + 1
  stat(s) = NF_INQ_VARID(ncid_in_t, 'temp', id_t)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading dimensions of ocean temperature file, no.', i
  END DO
  
! ******************************************************************
! ***  Open ocean biogeochemisty file                            ***
! ******************************************************************

  s = 1
  do b=biomin, biomax
  
    if (biofields(b) % dfield) CYCLE
  
    stat(s) = NF_OPEN(biofields(b)% filename, NF_NOWRITE, biofields(b)% ncid)
    IF (stat(s) /= NF_NOERR) WRITE(*, *) 'NetCDF error in reading biogeochemistry file, no.', s, ' (', biofields(b)% variable, ')'
    
    stat(s) = NF_INQ_VARID(biofields(b)% ncid, biofields(b)% variable, biofields(b)% fid)
    IF (stat(s) /= NF_NOERR) WRITE(*, *) 'NetCDF error in reading biogeochemistry field ID, no.', s, ' (', biofields(b)% variable, ')'
    s = s+1
          
  enddo

  ! Write dimensions
  WRITE (*,'(/1x,a)') 'Dimensions of model output:'
  WRITE (*,'(10x,1x,a25,i12)') 'nod2 (ssh, w, temp, salt)', nod2
  WRITE (*,'(10x,1x,a25,i12)') 'elem (u, v)              ', elem
  WRITE (*,'(10x,1x,a25,i12)') 'nz   (ssh, u, v, t, s)   ', nz
  WRITE (*,'(10x,1x,a25,i12)') 'nz+1 (w)                 ', nz+1
  WRITE (*,'(10x,1x,a25,i12)') 'nfields                  ', nfields
  WRITE (*,'(10x,1x,a25,i10)') 'time steps               ', steps

  IF (steps < ((maxtimes-1)*ostep+1)) THEN
     WRITE (*,'(1x,a)') '!! Number of available time slices is smaller than maxtimes - resetting !!'
     maxtimes = steps/ostep
  END IF
  WRITE (*,'(13x,a8,i10)') 'maxtimes', maxtimes
  
! ******************************************************************
! *** Define state vector                                        ***
! ******************************************************************
  
  ! Define field dimensions
  dim_fields (1) = nod2         ! Size of field SSH
  dim_fields (2) = nod2*nz      ! Size of field u (after interpolation)
  dim_fields (3) = nod2*nz      ! Size of field v (after interpolation)
  dim_fields (4) = nod2*(nz+1)  ! Size of field w
  dim_fields (5) = nod2*nz      ! Size of field temp
  dim_fields (6) = nod2*nz      ! Size of field salt
  dim_fields (7) = nod2         ! Size of field SIC
  
  ! Debug dimensions
  write(*,*) 'dim_fields(1) ', dim_fields(1)
  write(*,*) 'dim_fields(4) ', dim_fields(4)
  write(*,*) 'dim_fields(5) ', dim_fields(5)
  
  ! Biogeochemistry
  do b=biomin, biomax
     if (biofields(b) % ndims == 2) dim_fields(b) = nod2*nz
     if (biofields(b) % ndims == 1) dim_fields(b) = nod2
  enddo

 ! Define state dimension (physics)
  dim_state =    nod2 &
                 ! SSH
              +  nod2 * nz &
                 ! u (after interpolation)
              +  nod2 * nz &
                 ! v (after interpolation)
              +  nod2 * (nz+1) &
                 ! w
              +  nod2 * nz &
                 ! temp
              +  nod2 * nz &
                 ! salt
              +  nod2
                 ! SIC
                 
  ! Define state dimension (physics + biogeochemistry)
  dim_state = sum(dim_fields)

  WRITE (*,'(5x,a,i12)') 'dim_state', dim_state

  ! Define offsets
  offsets (1) = 0                        ! offset of field SSH
  offsets (2) = offsets(1) + nod2        ! offset of field u
  offsets (3) = offsets(2) + nod2*nz     ! offset of field v
  offsets (4) = offsets(3) + nod2*nz     ! offset of field w
  offsets (5) = offsets(4) + nod2*(nz+1) ! offset of field temp
  offsets (6) = offsets(5) + nod2*nz     ! offset of field salt
  offsets (7) = offsets(6) + nod2*nz     ! offset of field SIC
  
  ! Biogeochemistry
  do b=biomin, biomax
     offsets(b) = offsets(b-1) + dim_fields(b-1)
  enddo

! ****************************
! *** Read trajectory data ***
! ****************************

  WRITE (*,'(/1x,a)') '------- Read trajectory -------------'
  ALLOCATE(states(dim_state, maxtimes))
  ALLOCATE(u_orig(nz*elem))
  ALLOCATE(v_orig(nz*elem))
  
  read_in: DO iter = 1, maxtimes*ostep, ostep
 
  ! Read SSH (dim: nod2, time)
  IF (iter==1) THEN
  WRITE (*,'(/1x,a)') '- Read SSH at step 1'
  END IF
  
  startv2(1) = 1     
  countv2(1) = nod2  ! get nod2 (i.e. all) elements starting from 1
  startv2(2) = iter
  countv2(2) = 1     ! get one element at time step "iter"
  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_z, id_z, startv2, countv2, &
      states(1+offsets(1): offsets(2), iter/ostep+1))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading SSH'
  
  ! Read u (dim: nz, elem, time)
  IF (iter==1) THEN
  WRITE (*,*) '- Read u at step 1'
  END IF
  
  startv3(1) = 1
  countv3(1) = nz    ! get nz (i.e. all) elements starting from 1
  startv3(2) = 1     
  countv3(2) = elem  ! get elem (i.e. all) elements starting from 1
  startv3(3) = iter
  countv3(3) = 1     ! get one element at time step "iter"
  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_u, id_u, startv3, countv3, &
      u_orig)
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading u'

  ! Read v (dim: nz, elem, time)
  IF (iter==1) THEN
  WRITE (*,*) '- Read v at step 1'
  END IF
  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_v, id_v, startv3, countv3, &
      v_orig)
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading v'
      
  ! ******************************************************************
  ! *** Interpolation of u and v from elements onto nodes          ***
  ! ******************************************************************
  IF (iter==1) THEN
  WRITE (*,*) '- Interpolation of u and v at step 1'
  END IF
  
  DO n = 1,nod2
     DO l = 1, nlevels_nod2D(n)
        tvol=0.0
        tx  =0.0
        ty  =0.0
           DO c = 1, nod_in_elem2D_num(n)
               el = nod_in_elem2D(c,n)
               if (nlevels(el)<l) cycle
               tvol = tvol + elem_area(el)
               tx = tx + u_orig((el-1)*nz+l) *elem_area(el)
               ty = ty + v_orig((el-1)*nz+l) *elem_area(el)
           END DO
        states(offsets(2)+(n-1)*nz+l,iter/ostep+1) = tx/tvol ! u
        states(offsets(3)+(n-1)*nz+l,iter/ostep+1) = ty/tvol ! v
     END DO
  END DO

  ! Read w (dim: nz+1, nod2, time)
  IF (iter==1) THEN
  WRITE (*,*) '- Read w at step 1'
  END IF
  
  startv3(1) = 1     
  countv3(1) = nz+1  ! get nz+1 (i.e. all) elements starting from 1
  startv3(2) = 1
  countv3(2) = nod2  ! get nod2 (i.e. all) elements starting from 1
  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_w, id_w, startv3, countv3, &
      states(1+offsets(4): offsets(5), iter/ostep+1))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading w'

  ! Read temperature (dim: nz, nod2, time)
  IF (iter==1) THEN
  WRITE (*,*) '- Read temp at step 1'
  END IF
  
  startv3(1) = 1
  countv3(1) = nz
  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_t, id_t, startv3, countv3, &
      states(1+offsets(5): offsets(6), iter/ostep+1))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading temperature'

  ! Read salinity (dim: nz, nod2, time)
  IF (iter==1) THEN
  WRITE (*,*) '- Read salt at step 1'
  END IF
  
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_s, id_s, startv3, countv3, &
      states(1+offsets(6): offsets(6)+nod2*nz, iter/ostep+1))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading salinity'
  
  ! Read SIC (dim: nod2, time)
  IF (iter==1) THEN
  WRITE (*,*) '- Read SIC at step 1'
  END IF
  
  startv2(1) = 1     
  countv2(1) = nod2  ! get nod2 (i.e. all) elements starting from 1
  startv2(2) = iter
  countv2(2) = 1     ! get one element at time step "iter"
    
  stat(1) = NF_GET_VARA_DOUBLE(ncid_in_i, id_i, startv2, countv2, &
      states(offsets(7)+1 : offsets(7)+dim_fields(7), iter/ostep+1))
  IF (stat(1) /= NF_NOERR) &
      WRITE(*, *) 'NetCDF error in reading SIC'
      
  ! Read biogeochemistry
  ! surface fields
  startv2(1) = 1     
  countv2(1) = nod2  ! get nod2 (i.e. all) value starting from 1
  startv2(2) = iter
  countv2(2) = 1     ! get one value at time step "iter"
  
  ! 3D-fields
  startv3(1) = 1     
  countv3(1) = nz    ! get nz (i.e. all) values starting from 1
  startv3(2) = 1
  countv3(2) = nod2  ! get nod2 (i.e. all) values starting from 1
  startv3(3) = iter
  countv3(3) = 1     ! get one element at time step "iter"
       
  s=1
  do b=biomin, biomax
  
     if (biofields(b) % dfield) CYCLE
  
     IF (iter==1) WRITE(*,*) '- Read biogeochem. (',biofields(b)% variable,') at step 1'
  
     if (biofields(b)% ndims == 1) then
       ! surface fields
       stat(s) = NF_GET_VARA_DOUBLE(biofields(b)% ncid, biofields(b)% fid, startv2, countv2, &
       states(offsets(b)+1 : offsets(b)+dim_fields(b), iter/ostep+1))
     
     elseif (biofields(b)% ndims == 2) then
       ! 3D-fields       
       stat(s) = NF_GET_VARA_DOUBLE(biofields(b)% ncid, biofields(b)% fid, startv3, countv3, &
       states(offsets(b)+1 : offsets(b)+dim_fields(b), iter/ostep+1))
       
     endif ! surface / 3D-fields
     
     IF (stat(s) /= NF_NOERR) WRITE(*, *) 'NetCDF error in reading biogeochemistry, no. ', s, ' (', biofields(b)% variable, ')'
     s = s+1
     
  enddo ! biomin, biomax
      
  END DO read_in

  ! Close trajectory file

  stat(1) = nf_close(ncid_in_z)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing SSH trajectory file'

  stat(1) = nf_close(ncid_in_u)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing u-velocity trajectory file'

  stat(1) = nf_close(ncid_in_v)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing v-velocity trajectory file'

  stat(1) = nf_close(ncid_in_w)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing w-velocity trajectory file'

  stat(1) = nf_close(ncid_in_t)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing temperature trajectory file'

  stat(1) = nf_close(ncid_in_s)
  IF (stat(1) /= NF_NOERR) &
       WRITE(*, *) 'NetCDF error in closing salinity trajectory file'
       
  stat(1) = nf_close(ncid_in_i)
  IF (stat(1) /= NF_NOERR) &
  WRITE(*, *) 'NetCDF error in closing SIC trajectory file'
  
  s=1
  do b=biomin, biomax
  
    if (biofields(b) % dfield) CYCLE
    
    stat(s) = NF_CLOSE(biofields(b)% ncid)
    
    IF (stat(s) /= NF_NOERR) WRITE(*, *) 'NetCDF error in closing biogeochemistry file, no. ', s, ' (', biofields(b)% variable, ')'
    s = s+1
     
  enddo ! biomin, biomax

! *********************************************
! *** State trajectory of diagnostic fields ***
! *********************************************

  ! Mixed layer depth (dummy data)
  states(offsets(idbio% MLD1)  +1 : offsets(idbio% MLD1)  +dim_fields(idbio% MLD1)  , :) = 1
  
  ! Net primary production (dummy data)
  states(offsets(idbio% NPPn)  +1 : offsets(idbio% NPPn)  +dim_fields(idbio% NPPn)  , :) = 1
  states(offsets(idbio% NPPd)  +1 : offsets(idbio% NPPd)  +dim_fields(idbio% NPPd)  , :) = 1

  ! Total Chlorophyll
  states(offsets(idbio% TChl)  +1 : offsets(idbio% TChl)  +dim_fields(idbio% TChl)  , :) = &
  states(offsets(idbio% PhyChl)+1 : offsets(idbio% PhyChl)+dim_fields(idbio% PhyChl), :) + &
  states(offsets(idbio% DiaChl)+1 : offsets(idbio% DiaChl)+dim_fields(idbio% DiaChl), :)
  
  ! Total dissolved nitrogen
  states(offsets(idbio% TDN)+1 : offsets(idbio% TDN)+dim_fields(idbio% TDN), :) = &
  states(offsets(idbio% DIN)+1 : offsets(idbio% DIN)+dim_fields(idbio% DIN), :) + &
  states(offsets(idbio% DON)+1 : offsets(idbio% DON)+dim_fields(idbio% DON), :)
  
  ! Total organic carbon
  states(offsets(idbio% TOC) +1 : offsets(idbio% TOC) +dim_fields(idbio% TOC), :) = &
  states(offsets(idbio% PhyC)+1 : offsets(idbio% PhyC)+dim_fields(idbio% PhyC),:) + &
  states(offsets(idbio% DiaC)+1 : offsets(idbio% DiaC)+dim_fields(idbio% DiaC),:) + &
  states(offsets(idbio% DetC)+1 : offsets(idbio% DetC)+dim_fields(idbio% DetC),:) + &
  states(offsets(idbio% DOC) +1 : offsets(idbio% DOC) +dim_fields(idbio% DOC), :) + &
  states(offsets(idbio% HetC)+1 : offsets(idbio% HetC)+dim_fields(idbio% HetC),:)

! **********************************
! *** Compute running mean state ***
! **********************************

  WRITE (*,*) 'Compute running mean over', maxtimes,' snapshots at every ', ostep, '-th model output step'
  ALLOCATE(meanstate(dim_state))
  ALLOCATE(run_meanstate(dim_state, maxtimes))
  
  DO i = 1, maxtimes
     meanstate = 0.0

     ! "middle/main" part of timeseries
     IF (i > hwindow .AND. i <= (maxtimes - hwindow)) THEN
        Do j = i - hwindow, i + hwindow
           meanstate = meanstate + states (:, j)
        END DO

     ! near start of timeseries
     ELSE IF (i <= hwindow) THEN
        DO j = 1, i + hwindow
           meanstate = meanstate + states (:, j)
        END DO
        DO j = maxtimes - hwindow +i, maxtimes
           meanstate = meanstate + states (:, j)
        END DO

     ! towards end of timeseries
     ELSE IF (i > maxtimes - hwindow) THEN
        DO j = i - hwindow, maxtimes
           meanstate = meanstate + states (:, j)
        END DO        
        DO j = 1, i + hwindow - maxtimes
            meanstate = meanstate + states (:, j)
         END DO
     END IF
     
     meanstate = meanstate / (2 * hwindow + 1)
     run_meanstate (:, i) = meanstate
 
  END DO
  
  ! get residual
  states = states - run_meanstate  



! *********************************************************
! *** Singular value decomposition of covariance matrix ***
! ***                                                   ***
! *** The covariance matrix is given by the state       ***
! *** sequences X of k states as                        ***
! ***          -1    _     _ T        T                 ***
! *** P = (k-1)   (X-X) (X-X)  = U L U  (EVP)           ***
! ***                                                   ***
! *** Thus we compute the singular value decomposition  ***
! ***     _        T            -1    2  T              ***
! ***   X-X = U S V ;  P = (k-1)   U S  U               ***
! ***                                                   ***
! ***                         -1/2                      ***
! *** and we store U and (k-1)     S in a NetCDF file.  ***
! *********************************************************

  WRITE (*,'(/1x,a)') '------- Compute covariance matrix decomposition -------------'

  ! PDAF_eofcovar should compute and subtract the mean state from Trajectory 
  ! before decomposition. Afterwards, it's added again.
  remove_mean = 0

  ! Allocate arrays for singular values and vectors
  WRITE(*, *) 'Allocate svals'
  ALLOCATE(svals(maxtimes))
  
  WRITE(*, *) 'Allocate svdU'
  ALLOCATE(svdU(dim_state, maxtimes))
  
  WRITE(*, *) 'Call routine generating matrix decomposition'
  ! Call routine generating matrix decomposition
  CALL PDAF_eofcovar(dim_state, maxtimes, nfields, dim_fields, offsets, &
       remove_mean, do_mv, states, stddev, svals, svdU, meanstate, 1, status)
 
! *********************************************************
! *** Write mean state and decomposed covariance matrix ***
! *********************************************************

  ! *** Determine rank to write ***

  getlimit: DO i = 1, maxtimes
     IF (svals(i) >= limit) THEN
        rank = i
     ELSE
        EXIT getlimit
     END IF
  END DO getlimit
  IF (rank < maxtimes) THEN
     WRITE (*,'(1x,a,i6,a,es10.2)') &
          'Use maximum of ', rank, ' eigenvectors due to eigenvalue-limit of ',limit
  END IF
  IF (rank == maxtimes) THEN
     rank = maxtimes - 1
     WRITE (*,'(5x,a,i4)') '++ reset rank to ',rank
  END IF

  WRITE (*,'(5x,a)') 'Singular values: '
  DO i = 1, rank
    WRITE (*, '(10x, i4, es12.3)') i, svals(i)
  END DO


  WRITE (*,'(/1x,a)') '------- Write decomposed covariance matrix -------------'

  ! *** Initialize file
  s = 1
  !stat(s) = NF_CREATE(ncfile_out, IOR(NF_NOCLOBBER,NF_64BIT_OFFSET), ncid_out)
  stat(s) = NF_CREATE(ncfile_out, NF_64BIT_OFFSET, ncid_out)

  IF (do_mv == 1) THEN
     attstr = 'Running mean state, scaled singular vectors and values of multivariate decomposed covariance matrix for FESOM2.1'
  ELSE
     attstr = 'Running mean state, singular vectors and values of decomposed covariance matrix for FESOM2.1'
  END IF

  s = s + 1 ! 2
  stat(s) = NF_PUT_ATT_TEXT(ncid_out, NF_GLOBAL, 'title', LEN_TRIM(attstr), &
       TRIM(attstr))

  attstr = 'SSH, U, V, W, T, S, SIC, biogeochemistry'
  s = s + 1 ! 3
  stat(s) = NF_PUT_ATT_TEXT(ncid_out, NF_GLOBAL, 'state_fields', LEN_TRIM(attstr), &
       TRIM(attstr))
  
  s = s + 1 ! 4
  stat(s) = NF_DEF_DIM(ncid_out, 'rank',  rank, dimid_rank)
  s = s + 1 ! 5
  stat(s) = NF_DEF_DIM(ncid_out, 'nod2', dim_fields(1), dimid_2D) ! dim for SSH, SIC
  s = s + 1 ! 6
  stat(s) = NF_DEF_DIM(ncid_out, 'nz1_x_nod2', dim_fields(4), dimid_w) ! dim for w
  s = s + 1 ! 7
  stat(s) = NF_DEF_DIM(ncid_out, 'nz_x_nod2', dim_fields(5), dimid_tracer3D) ! dim for temp, salt and interpolated u and v velocities
  s = s + 1 ! 8
  stat(s) = NF_DEF_DIM(ncid_out, 'dim_state', dim_state, dimid_state)
  s = s + 1 ! 9
  stat(s) = NF_DEF_DIM(ncid_out, 'one',  1, dimid_one)
  s = s + 1 ! 10
  stat(s) = NF_DEF_DIM(ncid_out, 'nfields',  nfields, dimid_nfields)

  ! Define variables
  ! singular values
  s = s + 1 ! 11
  stat(s) = NF_DEF_VAR(ncid_out, 'sigma', NF_DOUBLE, 1, dimid_rank, Id_sigma)
 
  ! mean state
  
  dimids(1) = dimid_2D
  dimids(2) = dimid_one
  s = s + 1 ! 12
  stat(s) = NF_DEF_VAR(ncid_out, 'ssh_mean', NF_REAL, 2, dimids, Id_mz)
  s = s + 1 ! 13
  stat(s) = NF_DEF_VAR(ncid_out, 'a_ice_mean', NF_REAL, 2, dimids, Id_mi)

  dimids(1) = dimid_tracer3D
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'u_mean', NF_REAL, 2, dimids, Id_mu)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'v_mean', NF_REAL, 2, dimids, Id_mv)
  
  dimids(1) = dimid_w
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'w_mean', NF_REAL, 2, dimids, Id_mw)
  
  dimids(1) = dimid_tracer3D
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'temp_mean', NF_REAL, 2, dimids, Id_mt)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'salt_mean', NF_REAL, 2, dimids, Id_ms)
  s = s + 1
  
  ! mean states biogeochemistry
  do b=biomin, biomax
  
    if (biofields(b)% ndims==1) dimids(1) = dimid_2D
    if (biofields(b)% ndims==2) dimids(1) = dimid_tracer3D
    
    stat(s) = NF_DEF_VAR(ncid_out, TRIM(biofields(b)% variable)//'_mean', NF_REAL, 2, dimids, biofields(b)% mid)
    s = s + 1
  enddo

  ! singular vectors
  dimids(1) = dimid_2D
  dimids(2) = dimid_rank
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'ssh_svd', NF_REAL, 2, dimids, Id_svdz)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'a_ice_svd', NF_REAL, 2, dimids, Id_svdi)

  dimids(1) = dimid_tracer3D
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'u_svd', NF_REAL, 2, dimids, Id_svdu)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'v_svd', NF_REAL, 2, dimids, Id_svdv)
  
  dimids(1) = dimid_w
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'w_svd', NF_REAL, 2, dimids, Id_svdw)
  
  dimids(1) = dimid_tracer3D
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'temp_svd', NF_REAL, 2, dimids, Id_svdt)
  s = s + 1
  stat(s) = NF_DEF_VAR(ncid_out, 'salt_svd', NF_REAL, 2, dimids, Id_svds)

  ! singular vectors biogeochemistry
  do b=biomin, biomax
  
    if (biofields(b)% ndims==1) dimids(1) = dimid_2D
    if (biofields(b)% ndims==2) dimids(1) = dimid_tracer3D
    
    stat(s) = NF_DEF_VAR(ncid_out, TRIM(biofields(b)% variable)//'_svd', NF_REAL, 2, dimids, biofields(b)% sid)
    s = s + 1
  enddo

  ! End Define mode
  stat(s) = NF_ENDDEF(ncid_out)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) THEN
          WRITE(*, *) 'NetCDF error in init of output file, no.', i
          WRITE(*, *) NF_STRERROR(stat(i))
     ENDIF
  END DO

  ! Write singular values
  s = 1
  stat(s) = NF_PUT_VAR_DOUBLE(ncid_out, id_sigma, svals(1:rank))
  
  ! Write running mean (for the last snap shot)
  ! Write SSH part
  startv2(2) = 1
  countv2(2) = 1
  startv2(1) = 1
  countv2(1) = dim_fields(1)  
  s = s + 1
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_mz, startv2, countv2, &
          REAL(run_meanstate(1+offsets(1) : offsets(2), maxtimes), 4))

  ! Write U part
  countv2(1) = dim_fields(2)
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_mu, startv2, countv2, &
          REAL(run_meanstate(1+offsets(2) : offsets(3), maxtimes), 4))

  ! Write V part
  countv2(1) = dim_fields(3)
  s = s + 1
  stat(1) = NF_PUT_VARA_REAL(ncid_out, id_mv, startv2, countv2, &
          REAL(run_meanstate(1+offsets(3) : offsets(4), maxtimes), 4))

  ! Write W part
  countv2(1) = dim_fields(4)
  s = s + 1
  stat(1) = NF_PUT_VARA_REAL(ncid_out, id_mw, startv2, countv2, &
          REAL(run_meanstate(1+offsets(4) : offsets(5), maxtimes), 4))

  ! Write Temperature part
  countv2(1) = dim_fields(5)
  s = s + 1
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_mt, startv2, countv2, &
          REAL(run_meanstate(1+offsets(5) : offsets(6), maxtimes), 4))

  ! Write Salinity part
  countv2(1) = dim_fields(6)
  s = s + 1
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_ms, startv2, countv2, &
          REAL(run_meanstate(1+offsets(6) : offsets(6)+dim_fields(6), maxtimes), 4))
          
  ! Write SIC part
  countv2(1) = dim_fields(7)  
  s = s + 1
  stat(s) = NF_PUT_VARA_REAL(ncid_out, id_mi, startv2, countv2, &
          REAL(run_meanstate(1+offsets(7) : offsets(7)+dim_fields(7), maxtimes), 4))
          
  ! Write biogeochemistry part
  do b = biomin, biomax
       countv2(1) = dim_fields(b)
       s = s + 1
       stat(s) = NF_PUT_VARA_REAL(ncid_out, biofields(b)% mid, startv2, countv2, &
            REAL(run_meanstate(1+offsets(b) : offsets(b)+dim_fields(b), maxtimes), 4))
  enddo

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in writing running mean state, no.', i
  END DO


  ! *** Write singular vectors

  writevectors: DO i = 1, rank

     ! Write SSH part
     startv2(2) = i
     countv2(2) = 1
     startv2(1) = 1
     countv2(1) = dim_fields(1)
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdz, startv2, countv2, &
          REAL(svdU(1+offsets(1) : offsets(2), i), 4))

     ! Write U part
     countv2(1) = dim_fields(2)
     s = 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdu, startv2, countv2, &
          REAL(svdU(1+offsets(2) : offsets(3), i), 4))

     ! Write V part
     countv2(1) = dim_fields(3)
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdv, startv2, countv2, &
          REAL(svdU(1+offsets(3) : offsets(4), i), 4))

     ! Write W part
     countv2(1) = dim_fields(4)
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdw, startv2, countv2, &
          REAL(svdU(1+offsets(4) : offsets(5), i), 4))

     ! Write Temperature part
     countv2(1) = dim_fields(5)
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdt, startv2, countv2, &
          REAL(svdU(1+offsets(5) : offsets(6), i), 4))

     ! Write Salinity part
     countv2(1) = dim_fields(6)
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svds, startv2, countv2, &
          REAL(svdU(1+offsets(6) : offsets(6)+dim_fields(6), i), 4))
          
     ! Write SIC part
     countv2(1) = dim_fields(7)
     s = s + 1
     stat(s) = NF_PUT_VARA_REAL(ncid_out, id_svdi, startv2, countv2, &
          REAL(svdU(1+offsets(7) : offsets(7)+dim_fields(7), i), 4))
          
     ! Write biogeochemistry part
     do b = biomin, biomax
       countv2(1) = dim_fields(b)
       s = s + 1
       stat(s) = NF_PUT_VARA_REAL(ncid_out, biofields(b)% sid, startv2, countv2, &
            REAL(svdU(1+offsets(b) : offsets(b)+dim_fields(b), i),4))
     enddo

     DO k = 1,  s
        IF (stat(k) /= NF_NOERR) &
             WRITE(*, *) 'NetCDF error in writing singular vectors, no.', k,' rank',i
     END DO

  END DO writevectors

  ! Close file
  s = 1
  stat(s) = NF_CLOSE(ncid_out)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in singular vectors to output file, no.', i
  END DO


! ********************
! *** Finishing up ***
! ********************

  DEALLOCATE(states, meanstate)
  DEALLOCATE(svals, svdU)

  WRITE (*,'(/1x,a/)') '------- END -------------'

END PROGRAM generate_covar



















